plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Cloth Config
	maven {
		url "https://maven.shedaniel.me/"
	}

	// Trinkets, Cloth Config, Mod Menu
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}

	// Trinkets, Cardinal Components
	maven {
		name = "Ladysnake Libs"
		url = 'https://maven.ladysnake.org/releases'
	}

	// Stamina Attributes, Slot Customization API
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}

	// Mixin Squared
	maven {
		url 'https://jitpack.io'
	}

	// Azure Lib
	maven {
		url 'https://libs.azuredoom.com:4443/mods'
	}
}

loom {
	accessWidenerPath = file("src/main/resources/rpginventory.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cloth Config
	modImplementation ("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}")

	// Trinkets
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"

	// Azure Lib
	modImplementation("mod.azure.azurelib:azurelib-fabric-${project.minecraft_version}:${project.azure_lib_version}")

	// Mixin Squared
	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.1.2-beta.4")))

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

	// Stamina Attributes
	modImplementation "maven.modrinth:stamina-attributes:${project.stamina_attributes}"

	// Slot Customization API
	modImplementation "maven.modrinth:slot-customization-api:${project.slot_customization_api_version}"

	// Food Overhaul
	modImplementation "maven.modrinth:food-overhaul:${project.food_overhaul_version}"

	// Mana Attributes
	modImplementation "maven.modrinth:mana-attributes:${project.mana_attributes}"

	// Overhauled Damage
	modImplementation "maven.modrinth:overhauled-damage:${project.overhauled_damage_version}"

	// Health Regeneration Overhaul
	modImplementation "maven.modrinth:health-regeneration-overhaul:${project.health_regeneration_overhaul_version}"

}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = 'dF3krc92' // The ID of your Modrinth project. Slugs will not work.
	versionNumber = mod_version
	versionType = "release" // can also be "beta" or "alpha"
	uploadFile = remapJar
	gameVersions = ['1.20.1'] // An array of game versions the version supports
	loaders = ['fabric']
	dependencies = [
			new ModDependency('P7dR8mSH', 'required'), // Fabric API
			new ModDependency('9s6osm5g', 'required'), // Cloth Config
			new ModDependency('7zlUOZvb', 'required'), // Azure Lib
			new ModDependency('5aaWibi9', 'required'), // Trinkets
			new ModDependency('QKOE26gD', 'required'), // Stamina Attributes
			new ModDependency('QYZ7JTtD', 'required'), // Slot Customization API
			new ModDependency('UigDbuvI', 'optional'), // Food Overhaul
			new ModDependency('mOgUt4GM', 'optional') // Mod Menu
	]
	changelog = "${update_changelog}"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
